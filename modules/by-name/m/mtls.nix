{
  lib,
  lib',
  config,
  pkgs,
  ...
}:

let
  inherit (lib)
    mkIf
    optionalString
    mapAttrs'
    nameValuePair
    map
    attrNames
    attrValues
    concatStringsSep
    ;
  inherit (lib'.options)
    mkFalseOption
    mkDefault
    mkPathOption
    mkSubmoduleOption
    mkAttrsOf
    mkIntOption
    mkStrOption
    mkNullOr
    ;

  cfg = config.config'.mtls;

  dirCfg = {
    mode = "750";
    user = "mtls";
    group = "mtls";
  };

  caConfig = (pkgs.formats.ini { }).generate "ca.conf" {
    ca.default_ca = "CA_default";
    CA_default = {
      dir = cfg.dataDir;
      database = "${cfg.dataDir}/index.txt";
      crlnumber = "${cfg.dataDir}/crlnumber";
      new_certs_dir = "${cfg.dataDir}/newcerts";
      serial = "${cfg.dataDir}/serial";
      default_crl_days = "30";
      default_md = "sha256";
      policy = "policy_any";
    };
    policy_any.commonName = "supplied";
  };

  crlUpdateDeps = map (name: "mtls-client-setup-${name}.service") (attrNames cfg.clients);
in

{
  options.config'.mtls = {
    enable = mkFalseOption;
    dataDir = mkDefault "/var/lib/mtls" mkPathOption;
    p12DefaultPassword = mkDefault "default" mkStrOption;
    p12DefaultPasswordFile = mkNullOr mkPathOption;

    clients = mkAttrsOf (
      mkSubmoduleOption (
        { name, ... }:
        {
          options = {
            basePath = mkDefault "${cfg.dataDir}/clients/${name}" mkPathOption;
            daysValid = mkDefault 3650 mkIntOption;
            isRevoked = mkFalseOption;
            p12PasswordFile = mkNullOr mkPathOption;
          };
        }
      )
    );
  };

  config = mkIf cfg.enable {
    users.users.mtls = {
      home = cfg.dataDir;
      homeMode = "750";
      group = "mtls";
      isSystemUser = true;
    };

    users.groups.mtls = { };

    systemd.tmpfiles.settings.mtls = {
      "${cfg.dataDir}/newcerts".d = dirCfg;
      "${cfg.dataDir}/clients".d = dirCfg;
    }
    // mapAttrs' (_: client: nameValuePair client.basePath { d = dirCfg; }) cfg.clients;

    systemd.services = {
      mtls-setup = {
        wantedBy = [ "multi-user.target" ];

        path = [
          pkgs.openssl
          pkgs.coreutils
        ];
        script = ''
          openssl genpkey -algorithm RSA -pkeyopt rsa_keygen_bits:4096 -out ca.key
          openssl req -x509 -new -key ca.key -out ca.crt -days 3650 \
            -subj "/CN=mTLS Client CA"

          touch index.txt
          echo 01 > crlnumber
          echo 01 > serial

          openssl ca -gencrl -out ca.crl \
            -keyfile ca.key -cert ca.crt \
            -config ${caConfig}

          cat ca.crt ca.crl > ca.pem
        '';

        unitConfig.ConditionPathExists = [
          "!${cfg.dataDir}/ca.key"
          "!${cfg.dataDir}/ca.crt"
          "!${cfg.dataDir}/ca.crl"
          "!${cfg.dataDir}/ca.pem"
        ];
        serviceConfig = {
          User = "mtls";
          Group = "mtls";
          Type = "oneshot";
          WorkingDirectory = cfg.dataDir;
        };
      };

      mtls-crl-update = {
        wantedBy = [ "multi-user.target" ];
        wants = crlUpdateDeps;
        after = crlUpdateDeps;

        path = with pkgs; [
          openssl
          coreutils
          gnused
        ];
        script = ''
          ${concatStringsSep "\n" (
            map (client: ''
              if [ -f "${client.basePath}/client.crt" ];
              then
                SERIAL=$(openssl x509 -in "${client.basePath}/client.crt" -serial -noout | cut -d= -f2)
                ${optionalString client.isRevoked ''
                  grep -q "$SERIAL" index.txt \
                    || echo "R	$(date -d '+10 years' '+%y%m%d%H%M%SZ')	$(date '+%y%m%d%H%M%SZ')	$SERIAL	unknown	/CN=${client.name}" \
                    >> index.txt
                ''}
                ${optionalString (!client.isRevoked) ''
                  sed -i "/^R.*$SERIAL/d" index.txt
                ''}
              fi
            '') (attrValues cfg.clients)
          )}

          openssl ca -gencrl -out ca.crl \
            -keyfile ca.key -cert ca.crt \
            -config ${caConfig}

          cat ca.crt ca.crl > ca.pem
        '';

        serviceConfig = {
          User = "mtls";
          Group = "mtls";
          Type = "oneshot";
          WorkingDirectory = cfg.dataDir;
        };
      };
    }
    // mapAttrs' (
      name: client:

      let
        defaultPassword =
          if cfg.p12DefaultPasswordFile != null then
            "file:${cfg.p12DefaultPasswordFile}"
          else
            "pass:${cfg.p12DefaultPassword}";
        password =
          if client.p12PasswordFile != null then "file:${client.p12PasswordFile}" else defaultPassword;
      in

      nameValuePair "mtls-client-setup-${name}" {
        wantedBy = [ "multi-user.target" ];
        wants = [ "mtls-setup.service" ];
        after = [ "mtls-setup.service" ];

        path = [
          pkgs.openssl
          pkgs.util-linux
        ];
        script = ''
          openssl genpkey -algorithm RSA -pkeyopt rsa_keygen_bits:4096 \
            -out client.key

          openssl req -new \
            -key client.key \
            -out client.csr \
            -subj "/CN=${name}"

          exec 200> '${cfg.dataDir}/.ca.lock'
          flock -x 200

          openssl ca -batch -quiet \
            -in client.csr \
            -out client.crt \
            -days ${toString client.daysValid} \
            -keyfile '${cfg.dataDir}/ca.key' \
            -cert '${cfg.dataDir}/ca.crt' \
            -config ${caConfig}

          flock -u 200

          openssl pkcs12 -export -passout '${password}' \
            -inkey client.key \
            -in client.crt \
            -certfile '${cfg.dataDir}/ca.crt' \
            -out client.p12
        '';

        unitConfig.ConditionPathExists = [
          "${cfg.dataDir}/ca.key"
          "${cfg.dataDir}/ca.crt"
        ]
        ++ map (type: "!${client.basePath}/client.${type}") [
          "key"
          "csr"
          "crt"
          "p12"
        ];
        serviceConfig = {
          User = "mtls";
          Group = "mtls";
          Type = "oneshot";
          WorkingDirectory = client.basePath;
        };
      }
    ) cfg.clients;
  };
}
