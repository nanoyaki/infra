{
  inputs,
  lib,
  pkgs,
  config,
  ...
}:

let
  inherit (lib)
    nameValuePair
    mapAttrs'
    toLower
    attrNames
    filter
    hasPrefix
    listToAttrs
    ;
  inherit (inputs) copyparty;
  cfg = config.services.copyparty;

  defaults.flags = {
    fka = 32;
    dks = true;
  };
in

{
  imports = [ copyparty.nixosModules.default ];
  nixpkgs.overlays = [ copyparty.overlays.default ];

  sops.secrets = {
    "copyparty/hana".owner = cfg.user;
    "copyparty/sebi".owner = cfg.user;
    "copyparty/thomas".owner = cfg.user;
    "copyparty/ashley".owner = cfg.user;
    "copyparty/nik".owner = cfg.user;
  };

  systemd.services.copyparty.unitConfig.RequiresMountsFor = "/mnt/raid";
  systemd.services.copyparty.serviceConfig.RuntimeDirectoryMode = lib.mkForce "0770";
  services.copyparty = {
    enable = true;
    package = pkgs.copyparty.override { inherit (pkgs) partftpy; };
    mkHashWrapper = true;
    settings = {
      # Server options
      i = "unix:770:${cfg.group}:/run/copyparty/copyparty.sock";
      hist = "/var/cache/copyparty";
      shr = "/share";
      no-reload = true;
      name = "Theless.one files";
      theme = 0;

      # Password options
      chpw = true;
      ah-alg = "argon2";

      # Media options
      allow-flac = true;

      # Global options
      hardlink-only = true;
      magic = true;
      e2dsa = true;
      e2vp = true;
      df = "100g";
      xdev = true;
      xvol = true;
      grid = true;
      no-dot-ren = true;
      no-robots = true;
      force-js = true;
      og-ua = "Discordbot";
      fk = 24;
      dk = 48;
      chmod-f = 640;
      chmod-d = 750;
      ban-pw = "5,60,1440";
      grp-all = "acct";
      no-dupe = true;
      # usernames = true;
    };

    accounts = listToAttrs (
      map (
        attr:
        nameValuePair (pkgs.lib.nanolib.global.toUppercase (lib.removePrefix "copyparty/" attr)) {
          passwordFile = config.sops.secrets.${attr}.path;
        }
      ) (filter (attr: hasPrefix "copyparty/" attr) (attrNames config.sops.secrets))
    );

    volumes = {
      "/" = {
        path = "/mnt/raid/copyparty/root";
        access = {
          r = "@acct";
          A = "Hana";
        };
        inherit (defaults) flags;
      };

      "/shared" = {
        path = "/mnt/raid/copyparty/shared";
        access = {
          "rwmd." = "@acct";
          A = [
            "Hana"
            "Thomas"
          ];
        };
        inherit (defaults) flags;
      };

      "/shared-public-download" = {
        path = "/mnt/raid/copyparty/public";
        access = {
          "rwmd." = "@acct";
          A = [
            "Hana"
            "Thomas"
          ];
          g = "*";
        };
      };
    }
    // mapAttrs' (
      user: _:
      nameValuePair "/${toLower user}" {
        path = "/mnt/raid/copyparty-priv/${user}";
        access.A = user;
        inherit (defaults) flags;
      }
    ) cfg.accounts;
  };

  systemd.services.caddy.serviceConfig.BindPaths = [ "/run/copyparty/copyparty.sock" ];
  users.users.${config.services.caddy.user}.extraGroups = [ cfg.group ];
  config'.caddy.vHost.${config.config'.caddy.genDomain "files"}.extraConfig = ''
    reverse_proxy unix//run/copyparty/copyparty.sock
  '';

  config'.homepage.categories.Services.services.Copyparty = rec {
    description = "File server like dropbox/google drive";
    icon = "copyparty.svg";
    href = "https://files.theless.one";
    siteMonitor = href;
  };

  sops.secrets."restic/copyparty" = { };

  config'.restic.backups.copyparty = {
    repository = "/mnt/raid/backups/copyparty";
    passwordFile = config.sops.secrets."restic/copyparty".path;

    basePath = "/mnt/raid";
    paths = [
      "copyparty"
      "copyparty-priv"
    ];

    timerConfig.OnCalendar = "daily";
  };
}
